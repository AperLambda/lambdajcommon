apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'maven-publish'
apply plugin: 'signing'

group = 'org.aperlambda'
version = '1.7.1'
description = 'A library written in Java 8 with common features.'

sourceCompatibility = 8

ext.moduleName = 'org.aperlambda.lambdacommon'

compileJava.options.encoding = 'UTF-8'

repositories {
    mavenCentral()
}

dependencies {
    compile 'com.google.code.gson:gson:2.8.5'
    compile 'com.google.guava:guava:27.1-jre'
    compile 'org.jetbrains:annotations:17.0.0'
}

javadoc {
    inputs.property("moduleName", moduleName)
    doFirst {
        options.addStringOption('XDignore.symbol.file', '-html5')
        options.addStringOption('-module-path', classpath.asPath)
    }
}

jar {
    from 'LICENSE'
    inputs.property("moduleName", moduleName)
    manifest {
        attributes('Automatic-Module-Name': moduleName)
    }
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

/*signing {
    sign configurations.archives
}*/

publishing {
    publications {
        lambdajcommon(MavenPublication) {
            from components.java
            groupId = project.group
            artifactId = 'lambdajcommon'
            version = project.version

            artifact sourcesJar
            artifact javadocJar
        }
    }
    repositories {
        maven {
            url = "$buildDir/repo"
        }
    }
}
